# General outline of how this works:
#
# 1. Xray metadata code is inserted during asset compilation.
#    a. For Backbone.Views, JS is inserted that will record the Backbone.View
#       constructor with its metadata in window.XrayData
#    b. For templates, HTML comments are inserted at the beginning and the end
#       of the template.
#
# 2. After DOM is loaded, go through XrayData, hooking into Backbone.View to
#    add view instances to Xray.elements as they're instantiated. NOTE: This
#    means Xray has to be included into a page before any app code runs so its
#    DOM loaded callback can happen first. Not a big deal.
#
# 3. Xray is invoked via keyboard shortcut
#    a. Go through the DOM looking for template script tags. Create Elements
#       representing them. (regenerate on each invocation?)
#    b. Turn created Xray.elements into boxes and display them in the overlay.

window.Xray = {}

# Container for Xray.Element instances
Xray.elements = []

# Initialize Xray - hook into Backbone.View to create Xray.Elements as the views
# are instantiated.
Xray.init = ->
  return if Xray.initialized
  Xray.initialized = true

  if window.XrayData and window.Backbone?.View
    _delegateEvents = Backbone.View::delegateEvents
    Backbone.View::delegateEvents = ->
      _delegateEvents.apply(this, arguments)
      if info = Xray.constructorInfo(this.constructor)
        Xray.add this.el, JSON.parse(info)
    _remove = Backbone.View::remove
    Backbone.View::remove = ->
      _remove.apply(this, arguments)
      Xray.remove this.el

Xray.constructorInfo = (constructor) ->
  for own info, func of window.XrayData
    return info if func is constructor

# Scans the document for templates, creating Xray.Elements for them.
Xray.addTemplateElements = ->
  comments = $('*:not(iframe,script)').contents().filter ->
    this.nodeType == 8 and this.data[0..10] == " XRAY START"
  for comment in comments
    [_, id, path] = comment.data.match(/^ XRAY START (\d+) (.*) $/)
    $templateContents = new jQuery
    # Find the ending comment, adding any elements along the way
    el = comment.nextSibling
    until !el or (el.nodeType == 8 and el.data == " XRAY END #{id} ")
      if el.nodeType == 1 and el.tagName != 'SCRIPT'
        $templateContents.push el
      el = el.nextSibling
    Xray.add $templateContents,
      name: path.split('/').slice(-1)[0]
      path: path

# Open the given filesystem path by calling out to Xray's server.
# TODO: error handling of any kind, XSS warnings
Xray.open = (path) ->
  $.post "http://localhost:62010/open", path

# Add a DOM element with associated metadata to be tracked by Xray
Xray.add = (el, info = {}) ->
  unless !el || el.length == 0 || Xray.findElement(el)
    Xray.elements.push new Xray.Element(el, info)

# Remove a DOM element from Xray
Xray.remove = (el) ->
  if el instanceof Xray.Element
    Xray.elements.splice(Xray.elements.indexOf(el), 1)
  else
    Xray.remove Xray.findElement(el)

Xray.findElement = (el) ->
  el = el[0] if el instanceof jQuery
  for element in Xray.elements
    return element if element.el == el
  null

# Show the Xray overlay
Xray.show = ->
  Xray.Overlay.instance().show()

# Hide the Xray overlay
Xray.hide = ->
  Xray.Overlay.instance().hide()

# Wraps a DOM element that Xray is tracking
class Xray.Element
  constructor: (el, attrs = {}) ->
    @el = if el instanceof jQuery then el[0] else el
    @$el = $(el)
    @name = attrs.name
    @path = attrs.path

  isVisible: ->
    @$el.length and @$el.is(':visible')

  makeBox: ->
    bbox = @_computeBoundingBox()
    @$box = $('<div class="xray-box">').css
      top        : bbox.top
      left       : bbox.left
      width      : bbox.width
      height     : bbox.height
    if @$el.css('position') == 'fixed'
      @$box.css position: 'fixed'
    @$box.click =>
      # console.log @$el
      Xray.open @path
    @$box.append @name

  _computeBoundingBox: (el = @$el) ->
    @bbox = {}
    if el.length == 1
      offset = el.offset()
      # Handles an edge case where the container may not be fitted to its contents,
      # e.g. if its contents are floated.
      if el.height() == 0
        return @_computeBoundingBox(el.children())
      @bbox =
        left   : offset.left
        top    : offset.top
        width  : el.outerWidth()
        height : el.outerHeight()
    else if el.length > 1
      boxFrame =
        top    : Number.MAX_VALUE
        left   : Number.MAX_VALUE
        right  : 0
        bottom : 0
      for innerEl in el
        $el = $(innerEl)
        continue unless $el.is(':visible')
        frame = $el.offset()
        frame.right  = frame.left + $el.outerWidth()
        frame.bottom = frame.top + $el.outerHeight()
        boxFrame.top    = frame.top if frame.top < boxFrame.top
        boxFrame.left   = frame.left if frame.left < boxFrame.left
        boxFrame.right  = frame.right if frame.right > boxFrame.right
        boxFrame.bottom = frame.bottom if frame.bottom > boxFrame.bottom
      @bbox =
        left   : boxFrame.left
        top    : boxFrame.top
        width  : boxFrame.right - boxFrame.left
        height : boxFrame.bottom - boxFrame.top
    return @bbox

# Singleton class for the Xray "overlay" invoked by the keyboard shortcut
class Xray.Overlay
  @instance: ->
    @singletonInstance ||= new this

  constructor: ->
    @$overlay = $('<div id="xray-overlay">')
    @$overlay.click => @hide()
    @$style = $('<link rel="stylesheet" href="<%= asset_path("xray.css") %>">')

  show: ->
    Xray.isShowing = true
    $('head').append @$style
    $('body').append @$overlay
    @shownBoxes = []
    Xray.addTemplateElements()
    for element in Xray.elements
      continue unless element.isVisible()
      element.makeBox()
      element.$box.css('z-index' : 10000 + element.bbox.top)
      @shownBoxes.push element.$box
      $('body').append element.$box

  hide: ->
    Xray.isShowing = false
    @$overlay.detach()
    @$style.remove()
    $box.remove() for $box in @shownBoxes
    @shownBoxes = []



# Initialize on DOM ready.
$ -> Xray.init()

# Register keyboard shortcuts
$(document).keydown (e) ->
  if Xray.isShowing and e.keyCode is 27 # esc
    Xray.hide()
  if e.ctrlKey and e.metaKey and e.keyCode is 88 # cmd+ctrl+x
    if Xray.isShowing then Xray.hide() else Xray.show()
